load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_FEATURES",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "cmake_configure_file",
    "generate_include_header",
    "ign_config_header",
    "ign_export_header",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = IGNITION_FEATURES,
)

licenses(["notice"])

exports_files(["LICENSE"])

RESOURCE_PATH = "ign_rendering"

PROJECT_NAME = "ignition-rendering"

PROJECT_MAJOR = 6

PROJECT_MINOR = 0

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "config",
    src = "include/ignition/rendering/config.hh.in",
    out = "include/ignition/rendering/config.hh",
    cmakelists = ["include/ignition/CMakeLists.txt"],
    extra_defines = [
        "IGN_PROJECT_NAME=rendering",
        "IGN_RENDERING_RESOURCE_PATH=./ign_rendering/",
        "IGNITION_RENDERING_ENGINE_INSTALL_DIR=foo",
        "HAVE_OGRE2=1",
    ],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

ign_export_header(
    name = "include/ignition/rendering/Export.hh",
    export_base = "IGNITION_RENDERING",
    lib_name = "ignition-rendering",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/ignition/rendering/*.hh",
    "include/ignition/rendering/base/*.hh",
])

sources = glob(
    [
        "src/*.cc",
        "src/base/*.cc",
    ],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

generate_include_header(
    name = "renderinghh_genrule",
    out = "include/ignition/rendering.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/rendering/config.hh",
        "include/ignition/rendering/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/rendering/config.hh",
    "include/ignition/rendering/Export.hh",
    "include/ignition/rendering.hh",
]

cc_library(
    name = "ign_rendering",
    srcs = sources,
    hdrs = public_headers,
    defines = [
        "IGN_RENDERING_PLUGIN_PATH='\"ign_rendering/ogre2/\"'",
    ],
    includes = ["include"],
    deps = [
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/events",
        IGNITION_ROOT + "ign_common/graphics",
        IGNITION_ROOT + "ign_plugin/loader",
        "@X//:Xaw",
    ],
)

cmake_configure_file(
    name = "test_config",
    src = "test/test_config.h.in",
    out = "test/test_config.h",
    cmakelists = ["test/CMakeLists.txt"],
    defines = [
        "PROJECT_BINARY_DIR=./ign_rendering",
        "PROJECT_SOURCE_DIR=./ign_rendering",
        "CMAKE_BINARY_DIR=./ign_rendering",
    ],
    visibility = ["//visibility:private"],
)

cc_library(
    name = "test_utils",
    srcs = ["test/test_config.h"],
    data = [
        "test/media/materials/textures/texture.png",
        "test/media/meshes/walk.dae",
    ],
    includes = ["test"],
    visibility = ["//visibility:private"],
)

[
    cc_test(
        name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
        srcs = [src],
        data = [
            IGNITION_ROOT + "ign_rendering/ogre2:libignition-rendering-ogre2.so",
        ],
        defines = [
            "IGN_RENDERING_TEST_PLUGIN_PATH='\"ign_rendering/ogre2/\"'",
        ],
        deps = [
            ":ign_rendering",
            ":test_utils",
            "@gtest",
            "@gtest//:gtest_main",
        ],
    )
    for src in test_sources
]

[
    cc_test(
        name = "INTEGRATION_" + test.split("/")[2].replace(".cc", ""),
        srcs = [test],
        includes = [
            "test/integration",
        ],
        deps = [
            ":ign_rendering",
            ":test_utils",
            "@gtest",
            "@gtest//:gtest_main",
        ],
    )
    for test in glob(["test/integration/*.cc"])
]
